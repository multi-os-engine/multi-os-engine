/*
Copyright 2014-2016 Intel Corporation

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

apply plugin: 'java'

repositories {
    mavenCentral()
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

dependencies {
    testImplementation 'junit:junit:[4,)'
    implementation rootProject
    testImplementation files(System.getProperty("java.home") + "/../lib/tools.jar")
}

task processTestsClasses(type: JavaExec) {
    dependsOn(sourceSets.test.output)

    args "-Dretrolambda.inputDir=${buildDir}/classes/java/test"
    args "-Dretrolambda.classpath=${buildDir}/classes/java/test:../build/libs/natj-${version}.jar"
    args '-Dretrolambda.defaultMethods=true'
    args '-Dretrolambda.natjSupport=true'
    args "-Dretrolambda.outputDir=${buildDir}/classes/java/processedTestClasses"
    main '-jar'
    args 'tools/retrolambda-2.0.2.jar'

    inputs.dir("${buildDir}/classes/java/test")
    outputs.dir("${buildDir}/classes/java/processedTestClasses")
}

tasks.withType(Test) {
    def nativeConfiguration = 'Release'
    dependsOn tasks.processTestsClasses, ":natj-mac:build_TestClassesObjC_${nativeConfiguration}_macosx"

//    doFirst {
//        sourceSets.test.output.classesDir = 'build/classes/java/processedTestClasses'
//    }
    systemProperty 'java.library.path', file("../natj-mac/build/xcode/${nativeConfiguration}")
    testClassesDirs = processTestsClasses.outputs.files
    testLogging.showStandardStreams = true
    if (rootProject.hasProperty("moe.use.addresssanitizer")) {
        environment['DYLD_INSERT_LIBRARIES'] = '/Applications/Xcode.app/Contents/Developer/Toolchains/' +
                'XcodeDefault.xctoolchain/usr/lib/clang/8.0.0/lib/darwin/libclang_rt.asan_osx_dynamic.dylib'
        environment['ASAN_OPTIONS'] = 'handle_segv=0:allow_user_segv_handler=1'
    }
}
