/*
Copyright 2014-2016 Intel Corporation

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

apply plugin: 'maven-publish'

ext {
    nativeOutputs = []
}

def buildNativeTarget(xcodeproject, configuration, sdk, target) {
    task("build_${target}_${configuration}_${sdk}") {
        // Calculate product directory
        def productDest = "$buildDir/xcode/${configuration}-${sdk}"

        // Disable incremental build, leave it to Xcode
        inputs.property('timestamp', new Date().toString())

        // Set inputs and outputs
        inputs.dir rootProject.file('src')
        inputs.dir 'natj.xcodeproj'
        if (target == 'natj') {
            outputs.file "${productDest}/libnatj.a"
            project.nativeOutputs.plus "${productDest}/libnatj.a"
        }
        dependsOn ":prebuild_libffi_ios"

        // Prepare log file
        def log = file("${productDest}/build-${target}.log")
        mkdir log.parentFile
        outputs.file(log)

        // Add action
        doLast {
            try {
                exec {
                    executable 'xcodebuild'

                    args '-project', xcodeproject
                    args '-target', target
                    args '-sdk', sdk
                    args '-configuration', configuration

                    /*
                     * Uncomment to enable build with address sanitizer, Xcode 7+ required
                     */
                    // args 'OTHER_CFLAGS=$(OTHER_CFLAGS) -fsanitize=address'

                    setIgnoreExitValue false

                    FileOutputStream ostream = new FileOutputStream(log);
                    setErrorOutput(ostream)
                    setStandardOutput(ostream)
                }
            } catch (GradleException ex) {
                logger.error(log.text)
                throw new GradleException("Failed to build native components", ex)
            }
        }
    }
}

// Create build task
task build {
    dependsOn buildNativeTarget("natj.xcodeproj", "Release", "iphoneos", "natj")
    dependsOn buildNativeTarget("natj.xcodeproj", "Release", "iphonesimulator", "natj")
    dependsOn buildNativeTarget("natj.xcodeproj", "Debug", "iphoneos", "natj")
    dependsOn buildNativeTarget("natj.xcodeproj", "Debug", "iphonesimulator", "natj")
}

task buildAll {
    dependsOn build
}

// Create clean task
task clean {
    doLast {
        delete buildDir
    }
}

// Setup publishing
tasks.publish.dependsOn build
publishing {
    publications {
        mavenNative(MavenPublication) {
            artifactId 'natj-ios'

            for (o in nativeOutputs) {
                artifact source: o, extension: 'a', classifier: new File(o).getParentFile().getName()
            }
        }
    }
}
