/*
Copyright 2014-2016 Intel Corporation

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/


import org.gradle.internal.jvm.Jvm

apply plugin: 'java'

static def JvmVersionMeets(version) {
    def currentJvm = Jvm.current()
    return version <= Integer.parseInt(currentJvm.javaVersion.majorVersion)
}

targetCompatibility = "1.7"
sourceCompatibility = "1.7"

tasks.classes.dependsOn ':ext_natj_ios'

configurations {
    included
}

dependencies {
    compile group: 'junit', name: 'junit', version: '4.12'
    included group: 'junit', name: 'junit', version: '4.12'

    compile files(rootProject.external.natj.jar)
    compile project(':moe.apple:moe.platform.ios')
}

tasks.javadoc {
    if (JvmVersionMeets(8)) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    baseName 'moe-ios-junit'
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourceJar(type: Jar) {
    baseName 'moe-ios-junit'
    classifier = 'sources'
    from sourceSets.main.allSource
}

jar {
    baseName 'moe-ios-junit'
    from { configurations.included.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude 'META-INF/*.SF'
}

artifacts {
    archives javadocJar
    archives sourceJar
}
