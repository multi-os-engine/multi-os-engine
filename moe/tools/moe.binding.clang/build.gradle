/*
Copyright 2014-2016 Intel Corporation

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

// Versioning
ext {
    moeProject = 'MOE_BINDING_CLANG'
    moeVersion = [:]
}
initMOEProject(project, [vMajor: 3, vMinor: 8, vPatch: 0])

def CLANG_DYLIB_FILE = project.file('../../../external/llvm-build-minsizerel/lib/libclang.dylib')

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    natives
}

dependencies {
    compile fileTree(dir: 'lib', include: '*.jar')
    compile 'org.moe:natj-api:1.0.+'

    testCompile 'junit:junit:4.11'

    natives('org.moe:natj-mac:1.0.+') {
        artifact {
            name = 'natj-mac'
            extension = 'dylib'
            type = 'dylib'
            classifier = 'Release'
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

tasks.withType(Test) {
    forkEvery 0
    systemProperty "java.library.path", "build/prebuilt-natives"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'org.moe'
            artifactId 'moeBindingClang'

            from components.java

            //artifact(javadocJar)
            //artifact(sourceJar)

            artifact source: CLANG_DYLIB_FILE,
                    extension: "dylib",
                    classifier: 'libclang_moe_macosx_x86_64'
        }
    }
}

task copyNatJNative(type: Copy) {
    from configurations.natives
    into 'build/prebuilt-natives/'
    rename { 'libnatj.dylib' }
}
task copyClangNative(type: Copy) {
    from CLANG_DYLIB_FILE
    into 'build/prebuilt-natives/'
    rename { 'libclang_moe.dylib' }
}
test.dependsOn copyNatJNative, copyClangNative
