/*
Copyright (C) 2016 Migeran

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm"
    id "maven-publish"
}

if (name != 'moe-gradle') {
    throw new GradleException('Incorrect name')
}
group 'org.multi-os-engine.community'

String loadPluginVersion() {
    final Properties props = new Properties()
    props.load(new FileInputStream(file('src/main/resources/org/moe/gradle/moe.properties')))
    return props.getProperty('MOE-Plugin-Version')
}

version loadPluginVersion()

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    included

    compileOnly {
        extendsFrom included
    }

    testCompile {
        extendsFrom included
    }

    // Make sure the transitive dependencies are not packed into the jar
    included_no_transitive {
        extendsFrom included
        transitive = false
    }
}

dependencies {
    compile gradleApi()

    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    compile group: 'com.jcraft', name: 'jsch', version: '0.1.53'

    compile group: 'org.ow2.asm', name: 'asm', version: '9.1'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.7'

    compile group: 'com.googlecode.plist', name: 'dd-plist', version: '1.19'

    included project(':moe.tools.common')
    included project(':moe.document.pbxproj')
    included project(':moe.generator.project')
    included project(':moe.tools.substrate')
    included project(':moe.tools.classvalidator')

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile gradleTestKit()
}

// Add transitive dependencies into current project so they will be included into the pom file
def topDeps = configurations.included.resolvedConfiguration.firstLevelModuleDependencies
topDeps.each {topDep ->
    topDep.children.each { child ->
        if(!(child in topDeps)) {
            child.allModuleArtifacts.each { artifact ->
                def mvi = artifact.moduleVersion.id
                def dep = [
                        group  : mvi.group,
                        name   : mvi.name,
                        version: mvi.version,
                        ext    : artifact.extension
                ]
                if (artifact.classifier) {
                    dep['classifier'] = artifact.classifier
                }
                dependencies.compile(dep) {
                    transitive = true
                }
            }
        }
    }
}

tasks.test.dependsOn ':moe-sdk:devsdk'

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact(javadocJar)
            artifact(sourceJar)
            group project.group
            artifactId 'moe-gradle'
            version project.version
        }
        mavenJavaSnapshot(MavenPublication) {
            from components.java
            artifact(javadocJar)
            artifact(sourceJar)
            group project.group
            artifactId 'moe-gradle'
            version = "${project.version}-SNAPSHOT"
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/multi-os-engine-community/moe-plugin-gradle")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
}

jar {
    from { configurations.included_no_transitive.collect {it.isDirectory() ? it : zipTree(it) } }
}

// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = (sourceSets.main.runtimeClasspath + configurations.included).join("\n")
        file("$outputDir/plugin-sdk-localbuild.txt").text = project(':moe-sdk').file('build/dev-sdk')
        file("$outputDir/plugin-version.txt").text = project.version
    }
}

// Add the classpath file to the test runtime classpath
dependencies {
    testRuntime files(createClasspathManifest)
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}
