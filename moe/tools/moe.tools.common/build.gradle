/*
Copyright 2014-2016 Intel Corporation

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

apply plugin: 'java'
apply plugin: 'maven-publish'

compileJava {
    targetCompatibility = 1.6
    sourceCompatibility = 1.6
}

repositories {
    mavenLocal()
    mavenCentral()
}

// Versioning
ext {
    moeProject = 'MOE_TOOLS_COMMON'
    moeVersion = [:]
}
initMOEProject(project, [vMajor: 1, vMinor: 1, vPatch: 3])

sourceSets {
    testCommon {
        java { srcDir 'src/test-common/java' }
        resources { srcDir 'src/test-common/resources' }
    }
}

configurations {
    testOutput.extendsFrom(testCompile)
}

dependencies {
    compile fileTree(dir: 'lib', include: ['*.jar'])
    compile 'org.slf4j:slf4j-api:1.7.7'
    compile 'commons-io:commons-io:2.4'
    compile 'com.jcraft:jsch:0.1.53'
    compile 'com.jcraft:jzlib:1.1.3'
    testCompile 'junit:junit:4.11'
    testCommonCompile 'junit:junit:4.11'
}

jar {
    baseName = 'moeCommonUtils'
    doFirst {
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task testCommonJar(type: Jar) {
    classifier = 'test-common'
    from sourceSets.testCommon.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'org.moe'
            artifactId 'moeCommonUtils'

            from components.java

            artifact(testCommonJar)

            //artifact(javadocJar)
            //artifact(sourceJar)
        }
    }
}
