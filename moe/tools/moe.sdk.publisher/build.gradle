/*
Copyright (C) 2016 Migeran

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

buildscript {
    repositories {
        mavenCentral()
    }
}

group 'org.multi-os-engine'
version '1.9.1-SNAPSHOT'

apply plugin: 'base'
apply plugin: 'maven-publish'
apply plugin: 'signing'

def projectDeps = [
        ':moe.binding.clang',
        ':moe.binding.libimobiledevice',
        ':moe.generator.natjgen',
        ':moe.ios.device.launcher',
        ':proguard:app',
]

projectDeps.each { evaluationDependsOn it }

mkdir buildDir

task buildDeps {
    projectDeps.each { dependsOn project(it).tasks.build }
}

if (!rootProject.hasProperty('moe.tools.sdk.win_only')) {
    tasks.buildDeps.dependsOn ':ext_natj_mac'
    tasks.buildDeps.dependsOn ':prebuild_libimobiledevice_macos'
}

if (!rootProject.hasProperty('moe.tools.sdk.mac_only')) {
    tasks.buildDeps.dependsOn ':ext_natj_win'
    tasks.buildDeps.dependsOn ':prebuild_libimobiledevice_windows'
}

//////////////////////////////////////////////////////////////////////////
// Backport to Java 7
//////////////////////////////////////////////////////////////////////////

def backport_base_cfg = """
# Disable everything as we only want to backport
-dontshrink
-dontobfuscate
-dontoptimize
-dontwarn
-dontnote

# Backport to Java 7
-target 7
"""

task backportCore(type: JavaExec) {
    dependsOn buildDeps

    file("$buildDir/core.pro").createNewFile()

    ext.jarOutput = file("${buildDir}/libs/moe-core-backport.jar")

    doFirst {
        // Write rule
        file("$buildDir/core.pro").text = """
-injars "${rootProject.external.moe_core.out.moe_core_jar}"

-outjars "${jarOutput.absolutePath}"

${backport_base_cfg}
"""
    }

    main '-jar'
    args "${project(":proguard:app").buildDir.absolutePath}/libs/proguard.jar", "@${buildDir}/core.pro"

    inputs.file("$buildDir/core.pro")
    inputs.file(rootProject.external.moe_core.out.moe_core_jar)
    outputs.file(jarOutput)
}

task backportiOS(type: JavaExec) {
    dependsOn buildDeps

    file("$buildDir/ios.pro").createNewFile()

    ext.jarOutput = file("${buildDir}/libs/moe-ios-backport.jar")

    doFirst {
        // Write rule
        file("$buildDir/ios.pro").text = """
-injars "${rootProject.external.moe_core.out.moe_ios_jar}"

-libraryjars "${rootProject.external.moe_core.out.moe_core_jar}"

-outjars "${jarOutput.absolutePath}"

${backport_base_cfg}
"""
    }

    main '-jar'
    args "${project(":proguard:app").buildDir.absolutePath}/libs/proguard.jar", "@${buildDir}/ios.pro"

    inputs.file("$buildDir/ios.pro")
    inputs.file(rootProject.external.moe_core.out.moe_ios_jar)
    inputs.file(rootProject.external.moe_core.out.moe_core_jar)
    outputs.file(jarOutput)
}

task backportJUnit(type: JavaExec) {
    dependsOn buildDeps

    file("$buildDir/junit.pro").createNewFile()

    ext.jarOutput = file("${buildDir}/libs/moe-junit-backport.jar")

    doFirst {
        // Write rule
        file("$buildDir/junit.pro").text = """
-injars "${rootProject.external.moe_core.out.moe_ios_junit_jar}"

-libraryjars "${rootProject.external.moe_core.out.moe_core_jar}"
-libraryjars "${rootProject.external.moe_core.out.moe_ios_jar}"

-outjars "${jarOutput.absolutePath}"

${backport_base_cfg}
"""
    }

    main '-jar'
    args "${project(":proguard:app").buildDir.absolutePath}/libs/proguard.jar", "@${buildDir}/junit.pro"

    inputs.file("$buildDir/junit.pro")
    inputs.file(rootProject.external.moe_core.out.moe_ios_junit_jar)
    inputs.file(rootProject.external.moe_core.out.moe_core_jar)
    inputs.file(rootProject.external.moe_core.out.moe_ios_jar)
    outputs.file(jarOutput)
}

//////////////////////////////////////////////////////////////////////////
// Prebuild dex files
//////////////////////////////////////////////////////////////////////////

task processDexCore(type: Exec) {
    dependsOn backportCore

    def cmd = ["--dex"]

    cmd.add("--output")
    def dexOutput = file("$buildDir/moe-core.dex")
    cmd.add(dexOutput.absolutePath)
    ext.dexOutput = dexOutput
    outputs.file(dexOutput)

    cmd.add("--core-library")
    def jarInput = backportCore.jarOutput
    cmd.add(jarInput.absolutePath)
    inputs.file(jarInput)

    executable rootProject.external.moe_core.out.dx_exec
    args cmd
}

task processDexiOS(type: Exec) {
    dependsOn backportiOS

    def cmd = ["--dex"]

    cmd.add("--output")
    def dexOutput = file("$buildDir/moe-ios-dex.jar")
    cmd.add(dexOutput.absolutePath)
    ext.dexOutput = dexOutput
    outputs.file(dexOutput)

    cmd.add("--core-library")
    cmd.add("--multi-dex")
    def jarInput = backportiOS.jarOutput
    cmd.add(jarInput.absolutePath)
    inputs.file(jarInput)

    executable rootProject.external.moe_core.out.dx_exec
    args cmd
}

task processDexiOSJUnit(type: Exec) {
    dependsOn backportJUnit

    def cmd = ["--dex"]

    cmd.add("--output")
    def dexOutput = file("$buildDir/moe-ios-junit.dex")
    cmd.add(dexOutput.absolutePath)
    ext.dexOutput = dexOutput
    outputs.file(dexOutput)

    cmd.add("--core-library")
    def jarInput = backportJUnit.jarOutput
    cmd.add(jarInput.absolutePath)
    inputs.file(jarInput)

    executable rootProject.external.moe_core.out.dx_exec
    args cmd
}

task processSDK {
    dependsOn processDexCore
    dependsOn processDexiOS
    dependsOn processDexiOSJUnit
}

def buildfiles(def proj, def subpath, def closure) {
    files("${project(proj).buildDir.absolutePath}/$subpath", closure)
}

def getSDKCopySpec() {
    copySpec {
        into('sdk') {
            from files(rootProject.external.moe_core.out.moe_core_jar) {
                rename rootProject.external.moe_core.out.moe_core_jar.name, 'moe-core.jar'
            }
            from files(rootProject.external.moe_core.out.moe_core_sources_jar) {
                rename rootProject.external.moe_core.out.moe_core_sources_jar.name, 'moe-core-sources.jar'
            }
            from files(rootProject.external.moe_core.out.moe_core_javadoc_jar) {
                rename rootProject.external.moe_core.out.moe_core_javadoc_jar.name, 'moe-core-javadoc.jar'
            }
            from files(rootProject.external.moe_core.out.moe_ios_jar) {
                rename rootProject.external.moe_core.out.moe_ios_jar.name, 'moe-ios.jar'
            }
            from files(rootProject.external.moe_core.out.moe_ios_sources_jar) {
                rename rootProject.external.moe_core.out.moe_ios_sources_jar.name, 'moe-ios-sources.jar'
            }
            from files(rootProject.external.moe_core.out.moe_ios_javadoc_jar) {
                rename rootProject.external.moe_core.out.moe_ios_javadoc_jar.name, 'moe-ios-javadoc.jar'
            }
            from files(rootProject.external.moe_core.out.moe_ios_junit_jar) {
                rename rootProject.external.moe_core.out.moe_ios_junit_jar.name, 'moe-ios-junit.jar'
            }
            from files(rootProject.external.moe_core.out.moe_ios_junit_sources_jar) {
                rename rootProject.external.moe_core.out.moe_ios_junit_sources_jar.name, 'moe-ios-junit-sources.jar'
            }
            from files(rootProject.external.moe_core.out.moe_ios_junit_javadoc_jar) {
                rename rootProject.external.moe_core.out.moe_ios_junit_javadoc_jar.name, 'moe-ios-junit-javadoc.jar'
            }
            from files(processDexCore.dexOutput) {
                rename processDexCore.dexOutput.name, processDexCore.dexOutput.name
            }
            from files(processDexiOS.dexOutput) {
                rename processDexiOS.dexOutput.name, processDexiOS.dexOutput.name
            }
            from files(processDexiOSJUnit.dexOutput) {
                rename processDexiOSJUnit.dexOutput.name, processDexiOSJUnit.dexOutput.name
            }
            from files(rootProject.external.moe_core.out.moe_core_certificates_zip) {
                rename rootProject.external.moe_core.out.moe_core_certificates_zip.name, 'moe-core-certificates.zip'
            }
        }
        into('sdk/iphoneos/MOE.framework') {
            from files(rootProject.external.moe_core.out.iphoneos_MOE_framework)
        }
        into('sdk/iphonesimulator/MOE.framework') {
            from files(rootProject.external.moe_core.out.iphonesimulator_MOE_framework)
        }
        into('tools') {
            from files(rootProject.external.moe_core.out.dex2oat_exec.parentFile) {
                include rootProject.external.moe_core.out.dex2oat_exec.name
            }
            from files(rootProject.external.moe_core.out.dx_exec.parentFile) {
                include rootProject.external.moe_core.out.dx_exec.name
            }
            from files(rootProject.external.moe_core.out.dx_jar.parentFile) {
                include rootProject.external.moe_core.out.dx_jar.name
            }

            from buildfiles(':moe.ios.device.launcher', 'libs') {
                include 'moe.ios.device.launcher.jar'
                rename 'moe.ios.device.launcher.jar', 'ios-device.jar'
            }

            from files(rootProject.external.moe_core.out.java8support_jar.parentFile) {
                include rootProject.external.moe_core.out.java8support_jar.name
                include rootProject.external.moe_core.out.proguard_full_cfg.name
                include rootProject.external.moe_core.out.proguard_cfg.name
            }
            from files(rootProject.external.moe_core.out.preloaded_classes.parentFile) {
                include rootProject.external.moe_core.out.preloaded_classes.name
            }

            from buildfiles(':moe.generator.natjgen', 'libs') {
                include 'moe.generator.natjgen.jar'
                rename 'moe.generator.natjgen.jar', 'wrapnatjgen.jar'
            }

            from buildfiles(':proguard:app', 'libs') {
                include 'proguard.jar'
            }

            from files('.') {
                include 'gradlew.zip'
            }
        }

        into('tools/macosx') {
            if (!rootProject.hasProperty('moe.tools.sdk.win_only')) {
                from files(external.llvm.macos)
                from files(external.libimobiledevice.macos)
                from files(external.natj.mac)
            } else {
                from files('.placeholder')
            }
        }

        into('tools/windows/x86_64') {
            if (!rootProject.hasProperty('moe.tools.sdk.mac_only')) {
                from files(external.libimobiledevice.windows)
                from files(external.natj.win)
            } else {
                from files('.placeholder')
            }
        }

        eachFile { println "${it}" }
    }
}

task sdk(type: Zip, dependsOn: processSDK) {
    with getSDKCopySpec()
}

task devsdk(type: Copy, dependsOn: processSDK) {
    destinationDir = file("$buildDir/dev-sdk")
    with getSDKCopySpec()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact sdk {}
            artifactId = 'moe-sdk'

            pom {
                name = 'moe-sdk'
                packaging = 'zip'
                description = 'MOE sdk'
                url = 'https://discuss.multi-os-engine.org'

                scm {
                    url = 'https://github.com/multi-os-engine/multi-os-engine'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'MOE'
                        name = 'Multi-OS Engine Dev'
                        organization = 'Multi-OS Engine'
                        organizationUrl = 'https://www.multi-os-engine.org/'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = 'MavenCentral'
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                if (project.hasProperty('ossrhUsername')) {
                    username = ossrhUsername
                }
                if (project.hasProperty('ossrhPassword')) {
                    password = ossrhPassword
                }
            }
        }
    }
}

if (project.hasProperty("signing.keyId")) {
    signing {
        sign publishing.publications.mavenJava
    }
}
