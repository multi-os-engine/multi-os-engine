/*
 * Java binding for libimobiledevice and libplist.
 *
 * Copyright 2014-2016 Intel Corporation
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 2.1 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301  USA
 */

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

sourceCompatibility = "1.6"
targetCompatibility = "1.6"

// Versioning
ext {
    moeProject = 'MOE_BINDING_LIBIMOBILEDEVICE'
    moeVersion = [:]
}
initMOEProject(project, [vMajor: 1, vMinor: 1, vPatch: 0])

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'org.moe:natj-api:1.0.+'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'org.moe'
            artifactId 'moeBindingLibimobiledevice'

            from components.java

            //artifact(javadocJar)
            //artifact(sourceJar)

            artifact source: 'build/native/macosx/x86_64/libimobiledevice.dylib',
                    extension: "dylib",
                    classifier: 'libimobiledevice_macosx_x86_64'

            artifact source: 'build/native/windows/x86_64/libimobiledevice.dll',
                    extension: "dll",
                    classifier: 'libimobiledevice_windows_x86_64'

            artifact source: 'build/native/windows/x86_64/libeay32.dll',
                    extension: "dll",
                    classifier: 'libeay32_windows_x86_64'

            artifact source: 'build/native/windows/x86_64/ssleay32.dll',
                    extension: "dll",
                    classifier: 'ssleay32_windows_x86_64'
        }
    }
}

/*
Compile native lib.
 */
task compileLibimobiledeviceNative(type: Exec) {
    commandLine './build-native.sh'
}
if (!project.hasProperty("moe.native.skip")) {
    compileJava.dependsOn compileLibimobiledeviceNative
}
