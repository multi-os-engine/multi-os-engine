/*
Copyright 2014-2016 Intel Corporation

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/
plugins {
    id "com.diffplug.spotless" version "6.8.0"
}

apply plugin: 'java'

def JvmVersionMeets(version) {
    def currentJvm = org.gradle.internal.jvm.Jvm.current()
    return version <= Integer.parseInt(currentJvm.javaVersion.majorVersion)
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

tasks.classes.dependsOn ':ext_natj_ios'

dependencies {
    implementation "org.moe:natj"
    compileOnly 'org.jetbrains:annotations:23.0.0'
}

tasks.javadoc {
    if (JvmVersionMeets(8)) {
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addBooleanOption("-allow-script-in-comments", true)
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveBaseName = 'moe-ios'
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

task sourceJar(type: Jar) {
    archiveBaseName = 'moe-ios'
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
    from(rootProject.external.natj.sources) {
        include '**/*.java'
    }
}

jar {
    archiveBaseName = 'moe-ios'
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}

artifacts {
    archives javadocJar
    archives sourceJar
}

spotless {
    java {
        target("src/main/java/apple/**")
        removeUnusedImports()
        eclipse().configFile('Eclipse_Adjusted_NatJGen.xml')
    }
}
